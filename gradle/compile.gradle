tasks.withType(JavaCompile) {
    sourceCompatibility = versions.java
    targetCompatibility = versions.java

    configure(options) {
        compilerArgs << '-Xlint:all'            // Turn on all warnings
        compilerArgs << '-Xlint:-options'       // Turn off 'missing' bootclasspath warning
        compilerArgs << '-Xlint:-classfile'     // Espresso
        compilerArgs << '-Werror'               // Turn warnings into errors
        encoding = 'utf-8'
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
        showCauses true
        showExceptions true
        showStackTraces true
        events = ['failed', 'skipped']
    }

    failFast = true
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

tasks.withType(JavaForkOptions) {
    // Prevent forked processes from stealing focus (on MacOS at least)
    jvmArgs << '-XX:+UseCompressedOops'
    jvmArgs << '-Djava.awt.headless=true'
    jvmArgs << '-Dapple.awt.UIElement=true'
}
